plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group 'net.smoofyuniverse'
archivesBaseName = 'smoofyoreapi'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'SmoofyOreAPI'
                packaging 'jar'
                description "A simple java wrapper for Ore API."
                url 'https://github.com/Yeregorix/SmoofyOreAPI'

                scm {
                    connection 'scm:git:https://github.com/Yeregorix/SmoofyOreAPI.git'
                    developerConnection 'scm:git:https://github.com/Yeregorix/SmoofyOreAPI.git'
                    url 'https://github.com/Yeregorix/SmoofyOreAPI'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'Yeregorix'
                        name 'Hugo Dupanloup'
                        email 'yeregorix@smoofyuniverse.net'
                    }
                }
            }
        }
    }
}